/////////////
// GLOBALS //
/////////////
Texture2D shaderTexture : register(t0);
SamplerState SampleType : register(s0);

// LightBuffer 안에는 조명의 색과 방향을 담는 용도로 사용되는 두 전역 변수가 있음

cbuffer LightBuffer
{
    float ambientColor;
    float4 diffuseColor;
    float3 lightDirection;
    float specularPower;
    float4 specularColor;
};


//////////////
// TYPEDEFS //
//////////////
struct PixelInputType
{
    float4 position : SV_POSITION;
    float2 tex : TEXCOORD0;
    float3 normal : NORMAL;
    float3 viewDirection : TEXCOORD1;
};


////////////////////////////////////////////////////////////////////////////////
// Pixel Shader
////////////////////////////////////////////////////////////////////////////////
float4 LightPixelShader(PixelInputType input) : SV_TARGET
{
    float4 textureColor;
    float3 lightDir;
    float lightIntensity;
    float4 color;
    float3 reflection;
    float4 specular;


    // Sample the pixel color from the texture using the sampler at this texture coordinate location.
    textureColor = shaderTexture.Sample(SampleType, input.tex);
    
    // 여기서 조명 방정식이 구현됨
    // 빛의 조도(Intensity)가 삼각형의 법선 벡터, 그리고 빛의 방향 벡터 사이의 내적으로 계산됨
    
    // 결과의 색상값을 기본으로 주변광 값으로 맞춤
    // 이제 모든 픽셀들은 최소한 주변광 값을 가지게 됨
    // Set the default output color to the ambient light value for all pixels
    color = ambientColor;
    
    // Initialize the specular color
    specular = float4(0.0f, 0.0f, 0.0f, 0.0f);
    
    // Invert the light direction for calculations.
    lightDir = -lightDirection;

    // Calculate the amount of light on this pixel.
    lightIntensity = saturate(dot(input.normal, lightDir));
    
    // 법선과 빛의 방향의 내적이 0보다 큰지 확인
    // 만약 크다면 조명값을 주변광에 더하고, 그렇지 않다면 더하지 않아야 함
    // 이유는 간혹 음수로 계산되는 빛의 밝기 값이 주변광의 값을 깎아먹는 경우가 생기기 때문
    // (주변광 보다 어두운 부분이 생기는 경우가 생김)
    if (lightIntensity > 0.0f)
    {
        // Determine the final diffuse color based on the diffuse color and the amount of light intensity
        color += (diffuseColor * lightIntensity);
        
        // Saturate the ambient and diffuse color
        color = saturate(color);
        
        // 픽셀 셰이더에서의 정반사 벡터는 주어진 빛의 강도가 0보다 큰 경우에만 여기서 계산됨
        // Calculate the reflection vector based on the light intensity, normal vector, and light direction.
        reflection = normalize(2.0f * lightIntensity * input.normal - lightDir);
        
        // 정반사광의 총량은 반사 벡터와 보는 방향을 이용하여 계산됨
        // 관원과 카메라의 방향 사이의 각도가 작을수록 더 많은 정반사광이 비쳐 들어올 것임
        // 그런 결과는 specularPower 값 만큼의 제곱으로 나타나게 함
        // specularPower의 값이 작을수록 마지막에 빛이 더 많이 들어옴
        // Determine the amount of specular light based on the reflection vector, viewing direction, and specular power.
        specular = pow(saturate(dot(reflection, input.viewDirection)), specularPower);

    }

    // Multiply the texture pixel and the final diffuse color to get the final pixel color result.
    color = color * textureColor;
    
    // Add the specular component last to the output color.
    color = saturate(color + specular);

    return color;
}